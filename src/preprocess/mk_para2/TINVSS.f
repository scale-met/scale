      SUBROUTINE    TINVSS(N,A,DT,E,NN,IW,INDER)
C$NAME  TINVSS
C     INVERSION OF REAL MATRIX. SWEEP OUT, COMPLETE POSITIONING.
C--- HISTORY
C 71. 4.30 CREATED BY SAKATA MASATO
C 89.11.10 ADDED ERR
C 90. 1. 6 ERR*(*)
C--- INPUT
C N       I        DIMENSION OF THE MATRIX
C A     R(NN,N)    MATRIX
C NN      I        SIZE OF FIRST ARGUMENT
C E       R        CONVERGENCE CRITERION (0 IS OK)
C--- OUTPUT
C DT      R        DETERMINATION OF THE MATRIX
C IW    R(2*N)     WORKING AREA
C INDER   I        ERROR INDICATER
C$ENDI
C     INVERSION OF REAL MATRIX   A(NXN)
C     SWEEP OUT, COMPLETE POSITIONING
C
      IMPLICIT REAL*8(A-H,O-Z)
      DIMENSION     A(NN,N)  ,IW(*)
      INDER=0
      IF(N-1)    910,930,101
  101 IF(N.GT.NN)    GO TO  900
      EPS=0.0
      DT=1.0
      DO  100     K=1,N
      PIV=0.0
      DO  110       I=K,N
      DO  110       J=K,N
      IF(ABS(A(I,J)).LE.ABS(PIV))   GO TO  110
      IPIV=I
      JPIV=J
      PIV=A(I,J)
  110 CONTINUE
      DT=DT*PIV
      IF(ABS(PIV).LE.EPS)  GO TO 920
      IF(K.EQ.1)   EPS=ABS(PIV)*E
      IF(IPIV.EQ.K)      GO TO 130
      DT=-DT
      DO 120   J=1,N
      WORK=A(IPIV,J)
      A(IPIV,J)=A(K,J)
  120 A(K,J)=WORK
  130 IF(JPIV.EQ.K)      GO TO  150
      DT=-DT
      DO 140   I=1,N
      WORK=A(I,JPIV)
      A(I,JPIV)=A(I,K)
  140 A(I,K)=WORK
  150 IW(2*K-1)=IPIV
      AA=1.0/PIV
      IW(2*K)=JPIV
      DO 210   J=1,N
  210 A(K,J)=A(K,J)*AA
      DO 220  I=1,N
      IF(I.EQ.K)    GO TO  220
      AZ=A(I,K)
      IF(AZ.EQ.0.0)   GO TO  220
      DO 230   J=1,N
  230 A(I,J)=A(I,J)-A(K,J)*AZ
      A(I,K)=-AA*AZ
  220 CONTINUE
  100 A(K,K)=AA
      DO  400 KK=2,N
      K=N+1-KK
      IJ=IW(2*K)
      IF(IJ.EQ.K)   GO TO  420
      DO 410   J=1,N
      WORK=A(IJ,J)
      A(IJ,J)=A(K,J)
  410 A(K,J)=WORK
  420 IJ=IW(2*K-1)
      IF(IJ.EQ.K)   GO TO  400
      DO 430   I=1,N
      WORK=A(I,IJ)
      A(I,IJ)=A(I,K)
  430 A(I,K)=WORK
  400 CONTINUE
      RETURN
  910 INDER=-1
      WRITE(6,691)  N
  691 FORMAT(1H0,8H *** N = ,I5,5X,30HN SHOULD BE POSITIVE IN TINVSS)
      RETURN
  900 INDER=-1
      WRITE(6,690)    N,NN
  690 FORMAT(1H0,8H *** N =  ,I5,5X,4HNN =,I5/
     & 55H    N SHOULD BE LESS THAN OR EQUAL TO NN IN TINVSS       )
      RETURN
  920 DT=0.0
      INDER=N-K+1
      NNN=K-1
      WRITE(6,692)  NNN
  692 FORMAT(1H0,48H*** GIVEN MATRIX A TO TINVSS IS ILL CONDITIONED
     &,24HOR SINGULAR WITH RANK = ,I4,27H.   RETURN WITH NO FURTHER
     &,11HCALCULATION       )
      RETURN
  930 DT=A(1,1)
      K=1
      IF(DT.EQ.0.0)     GO TO  920
      A(1,1)=1.0/A(1,1)
      RETURN
      END
